plugins {
	id 'application'
	id 'maven-publish'
	id 'edu.sc.seis.launch4j' version '2.4.4'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['i18n', 'resources']
        }
    }
    test {
        java {
            srcDirs = ['unittests']
        }
        resources {
            srcDirs = ['testresources']
        }
    }
}

dependencies {
	implementation 'com.thoughtworks.xstream:xstream:1.4.10'
	implementation 'log4j:log4j:1.2.17'

	implementation 'javax.xml.bind:jaxb-api:2.3.0'
	runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:2.3.0'
	runtimeOnly 'org.glassfish.jaxb:jaxb-core:2.3.0'
	runtimeOnly 'com.sun.activation:javax.activation:1.2.0'

    testImplementation 'junit:junit:4.12' 
	testImplementation 'org.mockito:mockito-core:2.20.1'
}

mainClassName = 'megamek.MegaMek'

ext {
	dataDir = "data"
	confDir = "mmconf"
	logDir = "logs"
	docDir = "docs"
	libDir = "lib"
	distributionDir = "${buildDir}/distributions"
	fileStagingDir = "${buildDir}/files"
	atlasedImages = "${fileStagingDir}/atlasedImages.txt"
	unitFilesDir = "${dataDir}/mechfiles"
	ratDir = "${dataDir}/rat"
	scriptTemplate = "startScriptTemplate.txt"
}

jar {
	archiveName = "${rootProject.name}.${extension}"
	manifest {
	    attributes "Main-Class": mainClassName
	    attributes 'Class-Path' : project.sourceSets.main.runtimeClasspath.files
	    	.findAll { it.name.endsWith(".jar") }.collect { "${libDir}/${it.name}" }.join(' ')
	}
}

task officialUnitList(type: JavaExec, dependsOn: jar) {
	classpath = sourceSets.main.runtimeClasspath
	main = 'megamek.MegaMek'
	args '-oul'
}

task copyFiles(type: Copy, dependsOn: officialUnitList) {
	from projectDir
	
	include "${dataDir}/**"
	include "${confDir}/**"
	include "${docDir}/**"
	exclude "**/*.psd"
	exclude { it.file.isDirectory() && (it.file in file(unitFilesDir).listFiles()) }
	exclude "${ratDir}/**"
	
	into fileStagingDir
}

task createImageAtlases(type: JavaExec, dependsOn: copyFiles) {
	classpath = sourceSets.main.runtimeClasspath
	main = "megamek.utils.CreateImageAtlases"
	workingDir = file(fileStagingDir)
}

task deleteAtlasedImages(dependsOn: createImageAtlases) {
	doLast {
		ant.delete dir: fileStagingDir, includesfile: atlasedImages
		ant.delete file: atlasedImages
	}
}

task unitFilesZip (dependsOn: copyFiles) {
	file(unitFilesDir).eachDir {subDir ->
		dependsOn tasks.create("${name}${subDir.name}", Zip) {
			from subDir
			archiveName "${subDir.name}.zip"
			destinationDir file("${fileStagingDir}/${unitFilesDir}")
		}
	}
}

task ensureRatDir (dependsOn: copyFiles) {
	doLast {
		if (!file(fileStagingDir, ratDir).exists) {
			mkdir "${fileStagingDir}/${ratDir}"
		}
	}
}

task ratZip (type: Zip, dependsOn: ensureRatDir) {
	from ratDir
	archiveName "rat_default.zip"
	destinationDir file("${fileStagingDir}/${ratDir}")
}

task createLogDir (dependsOn: copyFiles) {
	doLast {
		mkdir "${fileStagingDir}/${logDir}"
		ant.touch(file: "${fileStagingDir}/${logDir}/timestamp")
	}
}

task stageFiles {
	dependsOn unitFilesZip
	dependsOn ratZip
	dependsOn deleteAtlasedImages
	dependsOn createLogDir
}

task unixStartScript (type: CreateStartScripts) {
	applicationName = startScripts.applicationName
	mainClassName = project.mainClassName
	outputDir = startScripts.outputDir
	classpath = jar.outputs.files + files(project.sourceSets.main.runtimeClasspath.files)
	    	.filter { it.name.endsWith(".jar") }
	// The default template assumes the start script is in a subdirectory and always
	// sets APP_HOME to the parent directory of the one containing the script.
	// So we provide a modified template.
	unixStartScriptGenerator.template = resources.text.fromFile(scriptTemplate)
	doLast {
		// The start script assumes all the files on the classpath will be in the lib directory.
		unixScript.text = unixScript.text.replace("lib/${rootProject.name}", rootProject.name)
	}
}

distributions {
	main {
		contents {
			from fileStagingDir
		}
	}
	windows {
		contents {
			from ("${buildDir}/launch4j") {
				include '*.exe'
			}
			contents {
				from fileStagingDir
				from (project.sourceSets.main.runtimeClasspath.files
	    				.findAll { it.name.endsWith(".jar") }) {
	    			into "${libDir}"
				}
			}
		}
	}
	unix {
		contents {
			from fileStagingDir
			from(unixStartScript.unixScript)
			from(jar)
			from (project.sourceSets.main.runtimeClasspath.files
    				.findAll { it.name.endsWith(".jar") }) {
    			into "${libDir}"
			}
		}
	}
}

distZip {
	dependsOn stageFiles
}

distTar {
	dependsOn stageFiles
	compression = Compression.GZIP 	
}

createExe {
	mainClassName = project.mainClassName
	icon = "${projectDir}/data/images/misc/megamek.ico"
}

windowsDistZip {
	dependsOn createExe
}

unixDistTar {
	dependsOn stageFiles
	dependsOn unixStartScript
	compression = Compression.GZIP
}

task deleteRepoDir (type: Delete) {
	delete = "${buildDir}/repo"
}

task cloneRepo (type: Exec, dependsOn: deleteRepoDir) {
	commandLine "git"
	args "clone", "--branch", "gradle", "--single-branch", gitRoot, "${buildDir}/repo"
}

task sourceZip (type: Zip) {
	from (rootDir) {
		exclude "**/build/**"
		exclude "**/.*"
	}
	archiveName "${rootProject.name.toLowerCase()}-${version}-source.zip"
	destinationDir file("${buildDir}/${distributionDir}")
}

task sourceTar (type: Tar) {
	from (rootDir) {
		exclude "**/build/**"
		exclude "**/.*"
	}
	archiveName "${rootProject.name.toLowerCase()}-${version}-source.tar.gz"
	compression = Compression.GZIP
	destinationDir file("${buildDir}/${distributionDir}")
}

task assembleDist(overwrite: true) {
	dependsOn unixDistTar
	dependsOn windowsDistZip
	dependsOn sourceZip
	dependsOn sourceTar
}

task buildFromRepo (type: GradleBuild) {
	dependsOn cloneRepo
	
	buildFile = "${buildDir}/repo/build.gradle"
	tasks = [ ':megamek:assembleDist' ]
}

task release (type: Copy) {
	dependsOn buildFromRepo
	
	from "${buildDir}/repo/${buildDir}/distributions"
	into "${buildDir}/distributions"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        publishMMLibrary(MavenPublication) {
            artifactId = 'megamek'
            from components.java
            artifact sourcesJar
// Javadoc is full of errors
//            artifact javadocJar

            pom {
                name = "MegaMek"
                description = "MegaMek library"
                url = "http://megamek.org"
                licenses {
                    license {
                        name = "GNU General Public License, version 2"
                        url = "https://www.gnu.org/licenses/gpl-2.0.html"
                    }
                }
            }
        }
	}
	repositories {
		mavenLocal()
	}
}
